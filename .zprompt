# ➤  ↵  »  ✔  ✗  ➜  ✚  ✹  ✖  ═  ✭  ⚡  ➭  ❮
# ▍ ♩ & ♫  ♪ ♬  ♭ ♮ ♯ ☺  ☻ ::  ⏎
# ♥  ♦ ♣  ♠  • ◘ ○ ◙  ♂ ♀
# ☭  , ⌘  , ☠, ⌥  , ✇ , ⌤  ,
# ⍜ , ✣ , ⌨   , ⌘, ☕
# ☮ ☠ ☻ ❀ ☃ ☆ ☄  ☢ ☉ ◎ ⊙ ░ ⍟
# ￬ ￪ ￫ ￩ ⇧ ⇩ ⇨ ⇦ ↑ ↓ ≠ ∞ ⿻  □ " ☼ ► ◄  ↕

function +vi-git-status {
    if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
        hook_com[unstaged]='%F{red}●%f'
    fi
}

function prompt_precmd {
    [[ -d ".git" ]] &&
        vcs_info || unset vcs_info_msg_0_
}

function do_prompt {
    local uc hc pc dc
    [[ -n $SSH_TTY ]] &&
    hc=yellow || hc=blue
	uc=${1:+"%F{$1}"}
	hc=${2:-$hc}
    pc=${3:-'g'}
    dc=${4:-'o'}

    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    add-zsh-hook precmd prompt_precmd

    zstyle ':vcs_info:*' enable git svn
    zstyle ':vcs_info:*' 'check-for-changes' true
    zstyle ':vcs_info:*' stagedstr '%F{g}●%f'
    zstyle ':vcs_info:*' unstagedstr '%F{y}●%f'
    zstyle ':vcs_info:*' formats '%F{g}%b%c%u%F{n}'
    zstyle ':vcs_info:*' actionformats "%b%c%u|%F{c}%a%f"
    zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{c}%r%f'
    zstyle ':vcs_info:git*+set-message:*' hooks git-status

    PROMPT='╭☾'${uc:-'%(#.%F{red}.%F{cyan})'}"%n%F{$dc}@%F{$hc%}%m%F{$dc}☽ %F{$pc}%0~%F{r}%(?..  ↵%?) %F{$dc}
╰─▶ "
    RPROMPT='${vcs_info_msg_0_}%F{n}'
    SPROMPT='zsh: correct %F{r}%R%f to %F{g}%r%f [nyae]? '
    PROMPT2=' ─▶ '
}

do_prompt "$@"
